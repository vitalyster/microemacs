; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2004-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created      Fri Jan 9 2004
; Synopsis:    Lua hook - invoked when a Lua file is loaded.
; Authors:     Jeremy Cowgar & Steven Phillips
;

define-macro fhook-lua
    set-variable $buffer-mask "luh1"
    @# buffer-init "lua"
    buffer-init-hooks
!emacro

; buffer-init variables
set-variable .fhook-lua.setup &reg "/history/fhook/lua" "bdfghinopx"
set-variable .fhook-lua.setup-mask "abdefghikmnoptux"
set-variable .fhook-lua.tags "luatags"
set-variable .fhook-lua.comment "|--|--|--| -- | -- |f|"
set-variable .fhook-lua.exact -1

; setup item-list
set-variable .fhook-lua.item-list-s1 "^[ \t]*function[ \t]+\\(\\w+\\)"
set-variable .fhook-lua.item-list-r1 "Func \ecB\\1\ecA"
set-variable .fhook-lua.item-list-s2 "^[ \t]*\\(\\w+\\)[ \t]*=[ \t]*function"
set-variable .fhook-lua.item-list-r2 "Func \ecB\\1\ecA"
set-variable .fhook-lua.item-list-s3 "^[ \t]*local[ \t]+\\(\\w+\\)[ \t]*=[ \t]*function"
set-variable .fhook-lua.item-list-r3 "LFnc \ecB\\1\ecA"

set-variable .fhook-lua.collapse-open   "^function\\|^[ \t]*\\(local[ \t]+\\)?\\w+[ \t]*=[ \t]*function"
set-variable .fhook-lua.collapse-close  "^end"
set-variable .fhook-lua.collapse-mclose "1"
set-variable .fhook-lua.collapse-mnext  "-1"

!if &not &exist .hilight.lua
    set-variable .hilight.lua &pinc .hilight.next 1
    set-variable .hilight.luaS &pinc .hilight.next 1
!endif
!if &and &sin "h" .fhook-lua.setup &band .hilight.flags 0x02
    ; define our highlighting scheme
    0 hilight .hilight.lua 1                    $global-scheme
    
    hilight .hilight.lua 2 "--"                 .scheme.comment
    
    hilight .hilight.lua 0x804 "\"" "\"" "\\"   .scheme.string
    hilight .hilight.lua 0x804 "'" "'" "\\"     .scheme.string
    hilight .hilight.lua 0x80 "\\[" .hilight.luaS .scheme.string
    hilight .hilight.lua 0 "\\w\\["             $global-scheme
    
    hilight .hilight.lua 1 "break"              .scheme.keyword
    hilight .hilight.lua 1 "do"                 .scheme.keyword
    hilight .hilight.lua 1 "else"               .scheme.keyword
    hilight .hilight.lua 1 "elseif"             .scheme.keyword
    hilight .hilight.lua 1 "end"                .scheme.keyword
    hilight .hilight.lua 1 "for"                .scheme.keyword
    hilight .hilight.lua 1 "function"           .scheme.keyword
    hilight .hilight.lua 1 "if"                 .scheme.keyword
    hilight .hilight.lua 1 "in"                 .scheme.keyword
    hilight .hilight.lua 1 "local"              .scheme.keyword
    hilight .hilight.lua 1 "repeat"             .scheme.keyword
    hilight .hilight.lua 1 "return"             .scheme.keyword
    hilight .hilight.lua 1 "then"               .scheme.keyword
    hilight .hilight.lua 1 "until"              .scheme.keyword
    hilight .hilight.lua 1 "while"              .scheme.keyword
    
    !if &band .hilight.flags 0x04
        hilight .hilight.lua 0 "^\\s*\\{function\\s+\\w+" .scheme.function
        hilight .hilight.lua 1 "\\w+\\} *= *function" .scheme.function
    !endif
    
    hilight .hilight.lua 1 "false"              .scheme.prepro
    hilight .hilight.lua 1 "true"               .scheme.prepro
    hilight .hilight.lua 1 "nil"                .scheme.prepro
    
    hilight .hilight.lua 1 "and"                .scheme.operator
    hilight .hilight.lua 1 "not"                .scheme.operator
    hilight .hilight.lua 1 "or"                 .scheme.operator
    
    hilight .hilight.lua 0 "\\+"                .scheme.operator
    hilight .hilight.lua 0 "-"                  .scheme.operator
    hilight .hilight.lua 0 "\\*"                .scheme.operator
    hilight .hilight.lua 0 "/"                  .scheme.operator
    hilight .hilight.lua 0 "\\^"                .scheme.operator
    hilight .hilight.lua 0 "~="                 .scheme.operator
    hilight .hilight.lua 0 "<="                 .scheme.operator
    hilight .hilight.lua 0 ">="                 .scheme.operator
    hilight .hilight.lua 0 "<"                  .scheme.operator
    hilight .hilight.lua 0 ">"                  .scheme.operator
    hilight .hilight.lua 0 "=="                 .scheme.operator
    hilight .hilight.lua 0 ";"                  .scheme.operator
    hilight .hilight.lua 0 ":"                  .scheme.operator
    hilight .hilight.lua 0 "\\."                .scheme.operator
    hilight .hilight.lua 0 "\\.\\."             .scheme.operator
    hilight .hilight.lua 0 "\\.\\.\\."          .scheme.operator
    
    ; String hilighting
    0 hilight .hilight.luaS 0                     .scheme.string
    hilight .hilight.luaS 0    "\\\\."            .scheme.string
    hilight .hilight.luaS 0   "\\[\\["            .scheme.string
    hilight .hilight.luaS 0   "\\]\\]"            .scheme.string
    hilight .hilight.luaS 0x80 "\\]" .hilight.lua .scheme.string
    ; let html based files use the lua hilight for any LUA Scripts
    set-variable .hilight.luascript .hilight.lua
!endif

!if &sin "d" .fhook-lua.setup
    ; define indentation scheme
    0 indent .hilight.lua 1 10
    
    indent .hilight.lua N "function" t
    indent .hilight.lua N "repeat"   t
    indent .hilight.lua O "until"   -t
    indent .hilight.lua N "do"       t
    indent .hilight.lua N "if"       t
    indent .hilight.lua S "else"    -t
    indent .hilight.lua S "else\\s*if" -t
    indent .hilight.lua O "end"     -t
    indent .hilight.lua n "{"        t
    indent .hilight.lua o "}"       -t
;    indent .hilight.lua c "[ \t]and[ \t]*\n" 6
;    indent .hilight.lua c "[ \t]or[ \t]*\n"  6
    indent .hilight.lua b "(" ")"
    indent .hilight.lua e "\"" "\"" "\\" 
    indent .hilight.lua e "'" "'" "\\" 
    indent .hilight.lua e "\\[" "\\]" "" 
    indent .hilight.lua i "--"
    ; let html based files use the lua indent for any LUA Scripts
    set-variable .indent.luascript .hilight.lua
!endif

buffer-init-fhook "lua"
